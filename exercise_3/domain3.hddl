(define (domain logistic_planning_3)
  (:requirements :typing :hierachie)

  (:types
    location - object ; can contain workstations robots and boxes
    warehouse - location ; contains supplies
    workstation - object ; can get boxes and contain supplies
    robot - object ; can pick up boxes, fill them, unload them and move between locations
    carrier - location ; contains boxes if it has free spaces
    space - object ; determines how many spaces a carrier has
    box - object ; can contain supplies
    supply - object ; required by workstations
    valve - supply ; type of supply
    bolt - supply ; type of supply
    tool - supply ; type of supply
  )

  (:predicates
    ;; locations
    (connected ?l1 ?l2 - location) ; location ?l1 is connected to location ?l2
    ;; boxes
    (box_at_loc ?b - box ?l - location) ; box ?b is in the location ?l
    (box_empty ?b - box) ; box ?b is empty
    (box_full ?b - box) ; box ?b is full
    (supply_in_box ?s - supply ?b - box) ; supply ?s is in box ?b
    ;; robots
    (robot_at_loc ?r - robot ?l - location) ; robot ?r is at location ?l
    (robot_has_carrier ?r - robot ?c - carrier) ; robot ?r has carrier ?c
    ;; workstations
    (ws_at_loc ?w - workstation ?l - location) ; workstation ?w is at location ?l
    ;; (ws_contains ?w - workstation ?s - supply) ; workstation ?w contains supply ?s
    (has_valve ?w - workstation) ; workstation ?w has a valve
    (has_bolt ?w - workstation) ; workstation ?w has a bolt
    (has_tool ?w - workstation) ; workstation ?w has a tool 
    (supply_available ?s - supply) ; supply ?s is available
    ;; supplies
    (supply_at_loc ?s - supply ?l - location) ; supply ?s is at location ?l
    ;; carrier space
    (free ?c - carrier ?s - space) ; space ?s is not used carrier ?c
    (occupied ?c - carrier ?s - space) ; space ?s is used by carrier ?c
  )

  ;; TODO: convert the 3 supply tasks into an unique task
  ;; TODO: find a way to create a method to bring a robot in a certain location

  (:task pick_up
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?sp - space 
      ?s - supply 
      ?l - location
    )
    :precondition ()
    :effect ()
  )
  (:task get_to
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?from - location 
      ?to - location
    )
    :precondition ()
    :effect ()
  )
  (:task deliver_valve
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?s - space 
      ?v - valve 
      ?w - workstation 
      ?l - location
    )
    :precondition ()
    :effect ()
  )
  (:task deliver_bolt
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?s - space 
      ?bo - bolt 
      ?w - workstation 
      ?l - location
    )
    :precondition ()
    :effect ()
  )
  (:task deliver_tool
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?s - space 
      ?t - tool 
      ?w - workstation 
      ?l - location
    )
    :precondition ()
    :effect ()
  )
  (:task valve_in_workstation
    :parameters (?v - valve ?w - workstation)
    :precondition ()
    :effect ()
  )
  (:task bolt_in_workstation
    :parameters (?bo - bolt ?w - workstation)
    :precondition ()
    :effect ()
  )
  (:task tool_in_workstation
    :parameters (?t - tool ?w - workstation)
    :precondition ()
    :effect ()
  )

  (:method m_prepare_robot
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?sp - space 
      ?s - supply 
      ?l - location
    )
    :task (pick_up ?r ?c ?b ?sp ?s ?l)
    :subtasks (and
      (task0 (load_carrier ?r ?c ?b ?sp ?l))
      (task1 (load_supply ?r ?l ?b ?s))
    )
    :ordering (and
      (task0 < task1)
    )
  )
  (:method m_move
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?from - location 
      ?to - location
    )
    :task (get_to ?r ?c ?from ?to)
    :subtasks (and
      (task0 (move_carrier ?r ?c ?from ?to))
    )
  )
  (:method m_unload_valve
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?s - space 
      ?v - valve 
      ?w - workstation 
      ?l - location
    )
    :task (deliver_valve ?r ?c ?b ?s ?v ?w ?l)
    :subtasks (and
      (task0 (unload_carrier ?r ?c ?b ?s ?l))
      (task1 (unload_valve ?r ?b ?v ?l ?w))
    )
    :ordering (and
      (task0 < task1)
    )
  )
  (:method m_unload_bolt
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?s - space 
      ?bo - bolt 
      ?w - workstation 
      ?l - location
    )
    :task (deliver_bolt ?r ?c ?b ?s ?bo ?w ?l)
    :subtasks (and
      (task0 (unload_carrier ?r ?c ?b ?s ?l))
      (task1 (unload_bolt ?r ?b ?bo ?l ?w))
    )
    :ordering (and
      (task0 < task1)
    )
  )
  (:method m_unload_tool
    :parameters (
      ?r - robot 
      ?c - carrier 
      ?b - box 
      ?s - space 
      ?t - tool 
      ?w - workstation 
      ?l - location
    )
    :task (deliver_tool ?r ?c ?b ?s ?t ?w ?l)
    :subtasks (and
      (task0 (unload_carrier ?r ?c ?b ?s ?l))
      (task1 (unload_tool ?r ?b ?t ?l ?w))
    )
    :ordering (and
      (task0 < task1)
    )
  )
  (:method m_deliver_valve
    :parameters (
      ?r - robot
      ?c - carrier
      ?b - box
      ?sp - space
      ?v - valve
      ?w - workstation
      ?from - location
      ?to - location
    )
    :task (valve_in_workstation ?v ?w)
    :subtasks (and
      (task0 (pick_up ?r ?c ?b ?sp ?v ?from))
      (task1 (get_to ?r ?c ?from ?to))
      (task2 (deliver_valve ?r ?c ?b ?sp ?v ?w ?to))
    )
    :ordering (and
      (task0 < task1)
      (task1 < task2)
    )
  )
  (:method m_deliver_bolt
    :parameters (
      ?r - robot
      ?c - carrier
      ?b - box
      ?sp - space
      ?bo - bolt
      ?w - workstation
      ?from - location
      ?to - location
    )
    :task (bolt_in_workstation ?bo ?w)
    :subtasks (and
      (task0 (pick_up ?r ?c ?b ?sp ?bo ?from))
      (task1 (get_to ?r ?c ?from ?to))
      (task2 (deliver_bolt ?r ?c ?b ?sp ?bo ?w ?to))
    )
    :ordering (and
      (task0 < task1)
      (task1 < task2)
    )
  )
  (:method m_deliver_tool
    :parameters (
      ?r - robot
      ?c - carrier
      ?b - box
      ?sp - space
      ?t - tool
      ?w - workstation
      ?from - location
      ?to - location
    )
    :task (tool_in_workstation ?t ?w)
    :subtasks (and
      (task0 (pick_up ?r ?c ?b ?sp ?t ?from))
      (task1 (get_to ?r ?c ?from ?to))
      (task2 (deliver_tool ?r ?c ?b ?sp ?t ?w ?to))
    )
    :ordering (and
      (task0 < task1)
      (task1 < task2)
    )
  )

  ;; moves a robot with its carrier to a new location
  (:action move_carrier
    :parameters (?r - robot ?c - carrier ?from ?to - location)
    :precondition (and
      (connected ?from ?to)
      (robot_at_loc ?r ?from)
      (robot_has_carrier ?r ?c)
    )
    :effect (and
      (not (robot_at_loc ?r ?from))
      (robot_at_loc ?r ?to)
    )
  )
  ;; loads the carrier of a robot with a box
  (:action load_carrier
    :parameters (?r - robot ?c - carrier ?b - box ?s - space ?l - location)
    :precondition (and
      (robot_at_loc ?r ?l)
      (box_at_loc ?b ?l)
      (robot_has_carrier ?r ?c)
      (free ?c ?s)
    )
    :effect (and
      (not (box_at_loc ?b ?l))
      (box_at_loc ?b ?c)
      (not (free ?c ?s))
      (occupied ?c ?s)
    )
  )
  ;; unloads the carrier of a robot with a box
  (:action unload_carrier
    :parameters (?r - robot ?c - carrier ?b - box ?s - space ?l - location)
    :precondition (and
      (robot_at_loc ?r ?l)
      (box_at_loc ?b ?c)
      (robot_has_carrier ?r ?c)
      (occupied ?c ?s)
    )
    :effect (and
      (box_at_loc ?b ?l)
      (not (box_at_loc ?b ?c))
      (free ?c ?s)
      (not (occupied ?c ?s))
    )
  )
  ;; loads an empty box with a supply
  (:action load_supply
    :parameters (?r - robot ?l - location ?b - box ?s - supply)
    :precondition (and
      (robot_at_loc ?r ?l)
      (box_at_loc ?b ?l)
      (supply_at_loc ?s ?l)
      (box_empty ?b)
      (supply_available ?s)
    )
    :effect (and
      (supply_in_box ?s ?b)
      (not (box_empty ?b))
      (box_full ?b)
      (not (supply_at_loc ?s ?l))
    )
  )
  ;; unloads the content of a box to a given workstation if it is a valve
  (:action unload_valve
    :parameters (?r - robot ?b - box ?valve - valve ?l - location ?w - workstation)
    :precondition (and
      (robot_at_loc ?r ?l)
      (supply_in_box ?valve ?b)
      (box_at_loc ?b ?l)
      (ws_at_loc ?w ?l)
      (box_full ?b)
      ;; (is_valve ?s)
    )
    :effect (and
      (box_empty ?b)
      (not (box_full ?b))
      (supply_at_loc ?valve ?l)
      ;; (ws_contains ?w ?valve)
      (not (supply_in_box ?valve ?b))
      (not (supply_available ?valve))
      (has_valve ?w)
    )
  )
  ;; unloads the content of a box to a given workstation if it is a bolt
  (:action unload_bolt
    :parameters (?r - robot ?b - box ?bolt - bolt ?l - location ?w - workstation)
    :precondition (and
      (robot_at_loc ?r ?l)
      (supply_in_box ?bolt ?b)
      (box_at_loc ?b ?l)
      (ws_at_loc ?w ?l)
      (box_full ?b)
      ;; (is_bolt ?bolt)
    )
    :effect (and
      (box_empty ?b)
      (not (box_full ?b))
      (supply_at_loc ?bolt ?l)
      ;; (ws_contains ?w ?bolt)
      (not (supply_in_box ?bolt ?b))
      (not (supply_available ?bolt))
      (has_bolt ?w)
    )
  )
  ;; unloads the content of a box to a given workstation if it is a tool
  (:action unload_tool
    :parameters (?r - robot ?b - box ?tool - tool ?l - location ?w - workstation)
    :precondition (and
      (robot_at_loc ?r ?l)
      (supply_in_box ?tool ?b)
      (box_at_loc ?b ?l)
      (ws_at_loc ?w ?l)
      (box_full ?b)
      ; (is_tool ?tool)
    )
    :effect (and
      (box_empty ?b)
      (not (box_full ?b))
      (supply_at_loc ?tool ?l)
      ;; (ws_contains ?w ?tool)
      (not (supply_in_box ?tool ?b))
      (not (supply_available ?tool))
      (has_tool ?w)
    )
  )

)